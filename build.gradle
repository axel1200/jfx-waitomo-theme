import org.gradle.internal.os.OperatingSystem

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.5'
}

version "${version}"

project.ext.javaFxPlatform = "${javaFxPlatform}"

allprojects {
  version "${version}"
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11

  repositories {
    jcenter()
  }
}

java {
  withSourcesJar()
  withJavadocJar()
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['mavenJava']
  pkg {
    repo = 'maven'
    name = 'jfx-waitomo-theme'
    licenses = ['GPL-3.0']
    vcsUrl = 'https://github.com/micheljung/jfx-waitomo-theme.git'
    githubRepo = 'micheljung/jfx-waitomo-theme'
    publicDownloadNumbers = true
  }
}

if (javaFxPlatform == "unspecified") {
  switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
      project.ext.javaFxPlatform = "linux"
      break
    case OperatingSystem.MAC_OS:
      project.ext.javaFxPlatform = "mac"
      break
    case OperatingSystem.WINDOWS:
      project.ext.javaFxPlatform = "win"
      break
  }
}

dependencies {
  compileOnly "org.openjfx:javafx-base:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-controls:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-fxml:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-graphics:${javaFxVersion}:${javaFxPlatform}"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'ch.micheljung.waitomo'
      artifactId 'jfx-waitomo-theme'
      version project.version
      from components.java
      pom {
        name = 'FxStage'
        description = 'Modern dark theme for JavaFX'
        url = 'https://github.com/micheljung/jfx-waitomo-theme'
        licenses {
          license {
            name = 'GNU General Public License, Version 3.0'
            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
          }
        }
        developers {
          developer {
            id = 'mj'
            name = 'Michel Jung'
            email = 'michel.jung89@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/micheljung/jfx-waitomo-theme.git'
          developerConnection = 'scm:git:git@github.com:micheljung/jfx-waitomo-theme.git'
          url = 'https://github.com/micheljung/jfx-waitomo-theme'
        }
      }
    }
  }
}